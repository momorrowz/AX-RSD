OUTPUT_FORMAT("elf32-littleriscv");
OUTPUT_ARCH("riscv")

/* エントリポイント．_start は rsd-crt.s で定義 */
ENTRY(_start);


MEMORY {
    ROM(rxai) :     ORIGIN = 0x00001000, LENGTH = 8m
    VRAM(wa) :      ORIGIN = 0x20000000, LENGTH = 8m
    RAM(wa) :       ORIGIN = 0x80000000, LENGTH = 48m
}

SECTIONS
{
    .text : { *(.text) *(.text.*) }     > ROM
    .rodata : { *(.rodata) *(.rodata.*) } > ROM
    .srodata : { *(.srodata) *(.srodata.*) } > ROM
    __rodata_end = .;
    .data : { 
      __data_start = .;
      *(.data)
      *(.data.*)
      *(.sdata)
      *(.sdata.*)
      __data_end = .;
    } > RAM  AT> ROM 
    . = ALIGN(4);
    .bss : { 
      __bss_start = .;
      *(.bss)
      *(.bss.*)
      *(.sbss)
      *(.sbss.*)
      __bss_end = . ;
    } > RAM AT> ROM
    . = ALIGN(4);
    .vram : {
      __vram_start = .;
      *(.vram)
      *(.vram.*)
      __vram_end = .;
    } > VRAM
    
    .comment : { *(.comment) } > ROM
}
